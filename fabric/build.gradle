plugins {
    id "me.modmuss50.mod-publish-plugin" version "0.4.5"
    id 'java'
    id 'idea'
    id 'maven-publish'
    id 'fabric-loom'
}
base {
    archivesName = "${mod_id}-fabric-${jar_version}"
}
dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
    implementation project(":common")

    include(implementation(annotationProcessor("io.github.llamalad7:mixinextras-fabric:0.3.2")))
}

loom {
    if (project(":common").file("src/main/resources/${mod_id}.accesswidener").exists()) {
        accessWidenerPath.set(project(":common").file("src/main/resources/${mod_id}.accesswidener"))
    }
    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
        defaultRefmapName.set("${mod_id}.fabric.refmap.json")
    }
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    source(project(":common").sourceSets.main.allSource)
}
tasks.withType(Javadoc).configureEach {
    source(project(":common").sourceSets.main.allJava)
}
tasks.named("sourcesJar", Jar) {
    from(project(":common").sourceSets.main.allSource)
}

processResources {
    from project(":common").sourceSets.main.resources
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId base.archivesName.get()
            from components.java
        }
    }
    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}

publishMods {
    file = remapJar.archiveFile
    changelog = "Initial release!"
    type = STABLE
    modLoaders.add("fabric")
    displayName = "%s ~ Fabric 1.20.1".formatted(project.version)

    modrinth {
        accessToken = providers.environmentVariable("CELESTIUM_MR")
        projectId = "GJZMhTIP"
        minecraftVersions.add("1.20.1")
    }

    //curseforge {
    //    accessToken = providers.environmentVariable("CELESTIUM_CF")
    //    projectId = "686836"
    //    minecraftVersions.add("1.20.1")
    //}
}